## Destructive Binary Operations, dreg/preg with 7bit immediate (CompI2op)
##
## +---+---+---+---|---+---+---+---|---+---+---+---|---+---+---+---+
## | 0 | 1 | 1 | 0 |.r.|opc|............src............|....dst....|
## +---+---+---+---|---+---+---+---|---+---+---+---|---+---+---+---+

### Tokens ###

define token ci2Instr16 (16)
	ci2Sig           = (12,15) 
	ci2R             = (11,11) 
	ci2Opc           = (10,10) 
	ci2SrcSImm       = ( 3, 9) signed
	ci2DstDReg       = ( 0, 2) 
	ci2DstPReg       = ( 0, 2) 
;


### Variables ###

attach variables ci2DstDReg [R0 R1 R2 R3 R4 R5 R6 R7];
attach variables ci2DstPReg [P0 P1 P2 P3 P4 P5 SP FP];


### Instructions ###

CompI2op:^"LdImmToReg" ci2DstDReg" = "ci2SrcSImm" (X)"
	is ci2Sig=0x6 & ci2R=0x0 & ci2Opc=0x0 & ci2SrcSImm & ci2DstDReg 
{
	ci2DstDReg = sext(ci2SrcSImm:1);
}

CompI2op:^"AddImm" ci2DstDReg" += "ci2SrcSImm
	is ci2Sig=0x6 & ci2R=0x0 & ci2Opc=0x1 & ci2SrcSImm & ci2DstDReg 
{
	ci2DstDReg = ci2DstDReg + ci2SrcSImm;
}

CompI2op:^"LdImmToReg" ci2DstPReg" = "ci2SrcSImm" (X)"
	is ci2Sig=0x6 & ci2R=0x1 & ci2Opc=0x0 & ci2SrcSImm & ci2DstPReg 
{
	ci2DstPReg = sext(ci2SrcSImm:1);
}

CompI2op:^"DagAddImm" ci2DstPReg" += "ci2SrcSImm
	is ci2Sig=0x6 & ci2R=0x1 & ci2Opc=0x1 & ci2SrcSImm & ci2DstPReg 
{
	ci2DstPReg = ci2DstPReg + ci2SrcSImm;
}



:^CompI2op is CompI2op { build CompI2op; }
